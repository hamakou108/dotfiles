# Note: only for MacOS

################################################################################
# Ensure that directories are unique within the PATH and MANPATH variables.
# The '-U' flag sets these variables to automatically remove any duplicate entries.
# cf. http://zsh.sourceforge.net/Doc/Release/Shell-Builtin-Commands.html#index-typeset
typeset -U PATH path MANPATH manpath

export LANG=en_US.UTF-8

alias ls='ls -FG'
alias la='ls -aFG'
alias ll='ls -lFG'

alias rm='rm -i'
alias cp='cp -i'
alias mv='mv -i'

alias mkdir='mkdir -p'

# Enable alist after "sudo"
alias sudo='sudo '

# Select keymap "emacs"
bindkey -e

# word-style settings
# cf. http://zsh.sourceforge.net/Doc/Release/User-Contributions.html#Widgets-1
autoload -Uz select-word-style && select-word-style default
# Given characters in word-chars are considered as break characters
# because word-style is set to unspecified.
zstyle ':zle:*' word-style unspecified
zstyle ':zle:*' word-chars " /=;@:{},|"

# Completion settings
# cf. http://zsh.sourceforge.net/Doc/Release/Completion-System.html
autoload -Uz compinit && compinit

# Ignore case
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# Ignore current directly after "../"
zstyle ':completion:*' ignore-parents parent pwd ..

# Suggest commands after "sudo"
zstyle ':completion:*:sudo:*' command-path /usr/local/sbin /usr/local/bin \
    /usr/sbin /usr/bin /sbin /bin /usr/X11R6/bin

# Suggest processes after "ps"
zstyle ':completion:*:processes' command 'ps x -o pid,s,args'

setopt extended_glob
setopt print_eight_bit

# History settings
HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=1000000

setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
#setopt share_history

# Enable to use wildcards by '*' when you search history by '^R'
bindkey '^R' history-incremental-pattern-search-backward

setopt ignore_eof
setopt interactive_comments
setopt no_beep
setopt no_flow_control

# XDG_CONFIG_HOME
export XDG_CONFIG_HOME=$HOME/.config

################################################################################
# Homebrew
if [[ -x /usr/local/bin/brew ]]; then
    local homebrew_prefix=$(brew --prefix)

    ################################################################################
    # Tmux
    alias tmux='tmux -2'
    export TERM=screen-256color

    ################################################################################
    # SSH
    alias ssh='TERM=screen ssh'

    ################################################################################
    # NeoVim
    if which nvim >/dev/null; then
        alias vi=$(which nvim)
        alias vim=$(which nvim)
        alias editor=$(which nvim)
    fi

    ################################################################################
    # GNU command utilities
    # The expression '(N-/)' represents the path is a directory or a symbolic link
    # which points to a directory (cf. zsh man page 14.8.7 Glob Qualifiers)
    path=(
        ${homebrew_prefix}/opt/coreutils/libexec/gnubin(N-/) # coreutils
        ${homebrew_prefix}/opt/ed/libexec/gnubin(N-/)        # ed
        ${homebrew_prefix}/opt/findutils/libexec/gnubin(N-/) # findutils
        ${homebrew_prefix}/opt/gnu-sed/libexec/gnubin(N-/)   # sed
        ${homebrew_prefix}/opt/gnu-tar/libexec/gnubin(N-/)   # tar
        ${homebrew_prefix}/opt/grep/libexec/gnubin(N-/)      # grep
        ${path}
    )
    manpath=(
        ${homebrew_prefix}/opt/coreutils/libexec/gnuman(N-/) # coreutils
        ${homebrew_prefix}/opt/ed/libexec/gnuman(N-/)        # ed
        ${homebrew_prefix}/opt/findutils/libexec/gnuman(N-/) # findutils
        ${homebrew_prefix}/opt/gnu-sed/libexec/gnuman(N-/)   # sed
        ${homebrew_prefix}/opt/gnu-tar/libexec/gnuman(N-/)   # tar
        ${homebrew_prefix}/opt/grep/libexec/gnuman(N-/)      # grep
        ${manpath}
    )

    coreutils_path="${homebrew_prefix}/opt/coreutils/libexec/gnubin"
    if [[ -d ${coreutils_path} ]]; then
        alias ls='ls -F --color=auto'
        alias la='ls -aF --color=auto'
        alias ll='ls -lF --color=auto'
    fi

    ################################################################################
    # asdf
    # TODO (add "asdf for oh-my-zsh" plugin)

    ################################################################################
    # OpenSSL
    path=(${homebrew_prefix}/opt/openssl@3/bin ${path})
fi

################################################################################
# Rust (rustup)
path=($HOME/.cargo/bin ${path})

################################################################################
# pipx
path=($HOME/.local/bin ${path})
